/******************************************************************************
 *
 * SAKSHAM RAJ
 *
 * 22/04/2023
 *
 *****************************************************************************/

MEMORY
{
    BOOT_FLASH (RX) : ORIGIN = 0x00000000, LENGTH = 16K
    BOOT_SRAM (RWX) : ORIGIN = 0x20000000, LENGTH = 8K
}

STACK_SIZE            = 1K;
HEAP_SIZE             = 1K;

REGION_ALIAS("REGION_TEXT", BOOT_FLASH);
REGION_ALIAS("REGION_BSS", BOOT_SRAM);
REGION_ALIAS("REGION_DATA", BOOT_SRAM);
REGION_ALIAS("REGION_STACK", BOOT_SRAM);
REGION_ALIAS("REGION_HEAP", BOOT_SRAM);


SECTIONS {

    PROVIDE (_intvecs_base_address = 0x00000000);

    .intvecs (_intvecs_base_address) : AT (_intvecs_base_address) {
        KEEP (*(.intvecs))
    } > REGION_TEXT

    PROVIDE (_vtable_base_address = 0x20000000);

    .vtable (_vtable_base_address) : AT (_vtable_base_address) {
        KEEP (*(.vtable))
    } > REGION_DATA

    .text : {              /* code and constants */
        . = ALIGN(4);
        *(.text)           /* .text sections (code) */
        *(.text*)          /* .text* sections (code) */
        *(.rodata)         /* .rodata sections (constants, strings, etc.) */
        *(.rodata*)        /* .rodata* sections (constants, strings, etc.) */

        KEEP (*(.init))
        KEEP (*(.fini))

        . = ALIGN(4);
    } > REGION_TEXT


    .preinit_array : {
        PROVIDE_HIDDEN (__preinit_array_start = .);
        KEEP (*(.preinit_array*))
        PROVIDE_HIDDEN (__preinit_array_end = .);
    } > REGION_TEXT

    .init_array : {
        PROVIDE_HIDDEN (__init_array_start = .);
        KEEP (*(SORT(.init_array.*)))
        KEEP (*(.init_array*))
        PROVIDE_HIDDEN (__init_array_end = .);
    } > REGION_TEXT

    .fini_array : {
        PROVIDE_HIDDEN (__fini_array_start = .);
        KEEP (*(.fini_array*))
        KEEP (*(SORT(.fini_array.*)))
        PROVIDE_HIDDEN (__fini_array_end = .);
    } > REGION_TEXT

    _etext = .;            /* global symbols at end of code */

    .data :  AT (_etext)
    {
        __data_load__ = LOADADDR (.data);
        __data_start__ = .;
        *(.data)           /* .data sections */
        *(.data*)          /* .data* sections */
        . = ALIGN(4);
        __data_end__ = .;
        _edata = __data_end__;
    } > REGION_DATA


    .bss :
    {
        __bss_start__ = .;
        *(.bss)
        *(.bss*)
        *(COMMON)
        . = ALIGN(4);
        _ebss = .;         /* define a global symbol at bss end */
        __bss_end__ = .;
    } > REGION_BSS


    .heap :
    {
        __heap_start__ = .;
        . = . + HEAP_SIZE;
        KEEP(*(.heap))
        . = ALIGN(4);
        __heap_end__ = .;
    } > REGION_HEAP


    .stack :
    {
        __stack_start__ = .;
        . = . + STACK_SIZE;
        KEEP(*(.stack))
        . = ALIGN(4);
        __stack_end__ = .;
    } > REGION_STACK
}

