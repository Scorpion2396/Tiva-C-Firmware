/******************************************************************************
 *
 * SAKSHAM RAJ
 *
 * 22/04/2023
 *
 *****************************************************************************/
_BOOT_FLASH_START_ADDR_  = 0x00000000;
_BOOT_SRAM_START_ADDR_   = 0x20000000;
_STACK_SIZE_             = 1K;
_HEAP_SIZE_              = 1K;

MEMORY
{
    BOOT_FLASH (RX) : ORIGIN = _BOOT_FLASH_START_ADDR_, LENGTH = 16K
    SRAM (RWX)      : ORIGIN = _BOOT_SRAM_START_ADDR_, LENGTH = 8K
}

REGION_ALIAS("REGION_TEXT", BOOT_FLASH);
REGION_ALIAS("REGION_BSS", SRAM);
REGION_ALIAS("REGION_DATA", SRAM);
REGION_ALIAS("REGION_STACK", SRAM);
REGION_ALIAS("REGION_HEAP", SRAM);


SECTIONS {

    PROVIDE (_intvecs_base_address = _BOOT_FLASH_START_ADDR_);

    .intvecs (_intvecs_base_address) : AT (_intvecs_base_address) {
        KEEP (*(.intvecs))
    } > REGION_TEXT

    PROVIDE (_vtable_base_address = _BOOT_SRAM_START_ADDR_);

    .vtable (_vtable_base_address) : AT (_vtable_base_address) {
        KEEP (*(.vtable))
    } > REGION_DATA

    .text : {
        CREATE_OBJECT_SYMBOLS
        *(.text)
        *(.text.*)
        . = ALIGN(0x4);
        KEEP (*(.ctors))
        . = ALIGN(0x4);
        KEEP (*(.dtors))
        . = ALIGN(0x4);
        __init_array_start = .;
        KEEP (*(.init_array*))
        __init_array_end = .;
        *(.init)
        *(.fini*)
    } > REGION_TEXT

    PROVIDE (__etext = .);
    PROVIDE (_etext = .);
    PROVIDE (etext = .);

    .rodata : {
        *(.rodata)
        *(.rodata*)
    } > REGION_TEXT

    .data : ALIGN (4)
    {
        __data_load__ = LOADADDR (.data);
        __data_start__ = .;
        *(.data)
        *(.data*)
        . = ALIGN (4);
        __data_end__ = .;
    } > REGION_DATA AT> REGION_TEXT


    .bss :
    {
        __bss_start__ = .;
        *(.shbss)
        *(.bss)
        *(.bss.*)
        *(COMMON)
        . = ALIGN (4);
        __bss_end__ = .;
    } > REGION_BSS

    .heap :
    {
        __heap_start__ = .;
        end = __heap_start__;
        _end = end;
        __end = end;
        KEEP(*(.heap))
        __heap_end__ = .;
        __HeapLimit = __heap_end__;
    } > REGION_HEAP

    .stack : ALIGN(0x8)
    {
        _stack = .;
        __stack = .;
        KEEP(*(.stack))
    } > REGION_STACK
}
